requires "x86-syntax.k"
requires "x86-configuration.k"
requires "x86-general-utils.k"

module X86-FLAG-CHECKS
  imports X86-SYNTAX
  imports X86-CONFIGURATION
  imports X86-GENERAL-UTILS

  /*@
    http://teaching.idallen.com/dat2343/10f/notes/040_overflow.txt
  */  
  
  /*@
    Update Overflow Flag.
  */  
  syntax KItem ::= updateOverflowAdd(MInt, MInt, MInt) 

  rule 
  <k> updateOverflowAdd(MIsrc1, MIsrc2, MIres) => . ...</k>
  <of> _ => fromBool( 
              eqMInt( 
                mi(bitwidthMInt(MIres),2), 
                addMInt(
                  xorMInt(
                    lshrMInt(MIsrc1, bitwidthMInt(MIres) -Int 1),
                    lshrMInt(MIres, bitwidthMInt(MIres) -Int 1)
                  ),
                  xorMInt(
                    lshrMInt(MIsrc2, bitwidthMInt(MIres) -Int 1),
                    lshrMInt(MIres, bitwidthMInt(MIres) -Int 1)
                  )
                )
              ) 
            , 1)
  </of>


  /*@
    Update Zero Flag.
  */  
  syntax KItem ::= updateZeroFlag(MInt) 
  rule 
  <k> updateZeroFlag(MI) => . ...</k>
  <zf> _ => fromBool(zeroMInt(MI),1) </zf>

  /*@
    Update Sign Flag.
  */  
  syntax KItem ::= updateSignFlag(MInt) 
  rule 
  <k> updateSignFlag(MI) => . ...</k>
  <sf> _ => fromBool( 
              eqMInt(
                mi(bitwidthMInt(MI),1), 
                lshrMInt(MI, bitwidthMInt(MI) -Int 1)) , 
              1) 
  </sf>

  /*@
    Update AuxCarry Flag.
  */  
  syntax KItem ::= updateAuxCarryFlag(MInt,MInt, MInt) 
  rule <k> updateAuxCarryFlag(MIsrc1, MIsrc2, MIres) => . ...</k>
  <af> _ =>  fromBool( 
              toBool(  
                andMInt( 
                  lshrMInt(
                    xorMInt( xorMInt( MIsrc1, MIsrc2), MIres), 
                    4
                  ),
                  mi(bitwidthMInt(MIres), 1)
                )
              )    
            , 1)
  </af>

  /*@
    Update Carry Flag.
  */  
  syntax Bool ::= carryHelperAdd(MInt,MInt, MInt) [function]
  rule  carryHelperAdd(MIsrc1, MIsrc2, MIres) => 
      ultMInt( MIres, MIsrc1) orBool ultMInt( MIres, MIsrc2)

  syntax KItem ::= updateCarryFlagAdd(MInt,MInt, MInt, MInt, MInt) 
  rule <k> updateCarryFlagAdd(MIsrc1, MIsrc2, MIsum, MIcarry, MIresult) => . ...</k>
  <cf> _ => fromBool( carryHelperAdd(MIsrc1, MIsrc2, MIsum) orBool carryHelperAdd(MIsum, MIcarry, MIresult) , 1)
  </cf>

  /*@
    Update Parity Flag.
  */  
  syntax KItem ::= updateParityFlag(MInt) 
  rule <k> updateParityFlag(MI:MInt) => . ...</k>
  <pf> _ => 
          fromBool(
            toBool(
              xorMInt(
                andMInt(
                  addListMInt(
                      32,
                      fromBool(toBool(andMInt(MI, mi(bitwidthMInt(MI),1))), 32)
                      fromBool(toBool(andMInt(MI, mi(bitwidthMInt(MI),2))), 32)
                      fromBool(toBool(andMInt(MI, mi(bitwidthMInt(MI),4))), 32)
                      fromBool(toBool(andMInt(MI, mi(bitwidthMInt(MI),8))), 32)
                      fromBool(toBool(andMInt(MI, mi(bitwidthMInt(MI),16))), 32)
                      fromBool(toBool(andMInt(MI, mi(bitwidthMInt(MI),32))), 32)
                      fromBool(toBool(andMInt(MI, mi(bitwidthMInt(MI),64))), 32)
                      fromBool(toBool(andMInt(MI, mi(bitwidthMInt(MI),128))), 32)
                  ), 
                  mi(32,1)),
                mi(32,1))
            ), 
          1)
  </pf>


  /*@
    Setting the flags with 0 or 1.
  */
  syntax KItem ::= setOverflow(MInt) 
  syntax KItem ::= setZeroFlag(MInt) 
  syntax KItem ::= setSignFlag(MInt) 
  syntax KItem ::= setAuxCarryFlag(MInt) 
  syntax KItem ::= setCarryFlag(MInt) 
  syntax KItem ::= setParityFlag(MInt) 

  rule 
  <k> setOverflow(MI) => . ...</k>
  <of> _ => MI </of>

  rule 
  <k> setZeroFlag(MI) => . ...</k>
  <zf> _ => MI </zf>

  rule 
  <k> setSignFlag(MI) => . ...</k>
  <sf> _ => MI </sf>

  rule 
  <k> setAuxCarryFlag(MI) => . ...</k>
  <af> _ => MI </af>

  rule 
  <k> setCarryFlag(MI) => . ...</k>
  <cf> _ => MI </cf>

  rule 
  <k> setParityFlag(MI) => . ...</k>
  <pf> _ => MI </pf>

endmodule  

module X86-REGISTER-SEMANTICS
  imports X86-FLAG-CHECKS
  //imports X86-REGISTER-OPERATIONS
  imports X86-SYNTAX
  imports X86-CONFIGURATION
  imports X86-GENERAL-UTILS

  /*@
    if the top computational task is to update a register, say Rd with a computed
    value I, the previous value of Rd, denoted by the wildcard _, is replaced by
    I, as shown below.
  */
  syntax KItem ::= updateReg(K, Register) [strict(1)]

  syntax Typeoperand ::= MInt

  // Autogenerated 
  rule <k> %rax => MI ...</k> <rax> MI </rax>
  rule <k> %rbx => MI ...</k> <rbx> MI </rbx>
  rule <k> %rcx => MI ...</k> <rcx> MI </rcx>
  rule <k> %rdx => MI ...</k> <rdx> MI </rdx>
  rule <k> %rsi => MI ...</k> <rsi> MI </rsi>
  rule <k> %rdi => MI ...</k> <rdi> MI </rdi>
  rule <k> %rsp => MI ...</k> <rsp> MI </rsp>
  rule <k> %rbp => MI ...</k> <rbp> MI </rbp>
  rule <k> %r8 => MI ...</k> <r8> MI </r8>
  rule <k> %r9 => MI ...</k> <r9> MI </r9>
  rule <k> %r10 => MI ...</k> <r10> MI </r10>
  rule <k> %r11 => MI ...</k> <r11> MI </r11>
  rule <k> %r12 => MI ...</k> <r12> MI </r12>
  rule <k> %r13 => MI ...</k> <r13> MI </r13>
  rule <k> %r14 => MI ...</k> <r14> MI </r14>
  rule <k> %r15 => MI ...</k> <r15> MI </r15>


  rule <k> updateReg(MI:MInt, %rax) => . ...</k> <rax> _ => MI </rax>
  rule <k> updateReg(MI:MInt, %rbx) => . ...</k> <rbx> _ => MI </rbx>
  rule <k> updateReg(MI:MInt, %rcx) => . ...</k> <rcx> _ => MI </rcx>
  rule <k> updateReg(MI:MInt, %rdx) => . ...</k> <rdx> _ => MI </rdx>
  rule <k> updateReg(MI:MInt, %rsi) => . ...</k> <rsi> _ => MI </rsi>
  rule <k> updateReg(MI:MInt, %rdi) => . ...</k> <rdi> _ => MI </rdi>
  rule <k> updateReg(MI:MInt, %rsp) => . ...</k> <rsp> _ => MI </rsp>
  rule <k> updateReg(MI:MInt, %rbp) => . ...</k> <rbp> _ => MI </rbp>
  rule <k> updateReg(MI:MInt, %r8) => . ...</k> <r8> _ => MI </r8>
  rule <k> updateReg(MI:MInt, %r9) => . ...</k> <r9> _ => MI </r9>
  rule <k> updateReg(MI:MInt, %r10) => . ...</k> <r10> _ => MI </r10>
  rule <k> updateReg(MI:MInt, %r11) => . ...</k> <r11> _ => MI </r11>
  rule <k> updateReg(MI:MInt, %r12) => . ...</k> <r12> _ => MI </r12>
  rule <k> updateReg(MI:MInt, %r13) => . ...</k> <r13> _ => MI </r13>
  rule <k> updateReg(MI:MInt, %r14) => . ...</k> <r14> _ => MI </r14>
  rule <k> updateReg(MI:MInt, %r15) => . ...</k> <r15> _ => MI </r15>


  /*@
    handleImmediate(Imm, W): Compute a MInt corresponding
    to the Imm value of width W.
  */

  // TODO: Need a way to convert HexConstant to String
  //syntax String ::= HexConstant
  //syntax Int ::= HexStringtToInt(String, Int)  [function, hook(STRING.string2base)]
  //rule handleImmediate($ H:HexConstant, W:Int) => 
  //                           mi(W, HexStringtToInt(H, 16)) 

  syntax MInt ::=  handleImmediate(Imm, Int) [function]
  rule handleImmediate($ I:Int, W:Int) => mi(W,I)
endmodule












